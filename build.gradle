plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'java-library'
apply plugin: 'maven-publish'

group pkgGroupId
version pkgVersion

ext {
    bintrayUser = project.hasProperty("bintrayUser") ? bintrayUser : "unknown"
    bintrayApiKey = project.hasProperty("bintrayApiKey") ? bintrayApiKey : "unknown"
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    api 'javax.inject:javax.inject:1'
    api 'com.google.code.findbugs:jsr305:2.0.1'
    api 'io.opentracing:opentracing-api:0.30.0'
    api 'io.opentracing:opentracing-util:0.30.0'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.jmockit:jmockit:1.30'
}

bintray {
    user = project.ext.bintrayUser
    key = project.ext.bintrayApiKey
    publications = ['mavenJava']
    pkg {
        repo = 'opentracing-basic'
        name = 'opentracing-basic'
        userOrg = project.ext.bintrayUser
        licenses = ['Apache-2.0']
        githubRepo = 'eBay/opentracing-basic'
        vcsUrl = 'git@github.com:eBay/opentracing-basic.git'
        websiteUrl = 'https://github.com/eBay/opentracing-basic'
        issueTrackerUrl = 'https://github.com/eBay/opentracing-basic/issues'
        publish = true

        version {
            name = pkgVersion
            vcsTag = "release/${pkgVersion}"
            released = new Date()
            gpg {
                sign = true
            }
        }
    }
}
task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    dependsOn tasks.javadoc
}

artifacts {
    archives sourceJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = DELEGATE_FIRST
                    name "${pkgGroupId}:${project.name}"
                    url 'https://github.com/eBay/opentracing-basic.git'
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    scm {
                        url 'https://github.com/eBay/opentracing-basic.git'
                        developerConnection 'scm:git:ssh://github.com:eBay/opentracing-basic.git'
                        connection 'git@github.com:eBay/opentracing-basic.git'
                    }

                    developers {
                        developer {
                            id 'mcumings'
                            name 'Mike Cumings'
                            email 'mcumings@ebay.com'
                        }
                    }
                }
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

