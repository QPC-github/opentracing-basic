apply plugin: 'java-library'
apply plugin: 'maven-publish'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    api 'javax.inject:javax.inject:1'
    api 'com.google.code.findbugs:jsr305:2.0.1'
    api 'io.opentracing:opentracing-api:0.30.0'
    api 'io.opentracing:opentracing-util:0.30.0'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.jmockit:jmockit:1.30'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = 'com.ebay.opentracing'
            artifactId = 'opentracing-basic'
            version = '0.0.0'
            artifact sourceJar
            artifact javadocJar


            pom.withXml {
                asNode().with {
                    appendNode('packaging', 'jar')
                    appendNode('name', 'opentracing-basic')
                    appendNode('description', 'Basic OpenTracing API implementation')
                    appendNode('scm').with {
                        appendNode('url', 'https://github.com/eBay/opentracing-basic.git')
                        appendNode('connection', 'git@github.com:eBay/opentracing-basic.git')
                    }
                    appendNode('licenses').with {
                        appendNode('license').with {
                            appendNode('name', 'Apache License, Version 2.0')
                            appendNode('url', 'https://www.apache.org/licenses/LICENSE-2.0')
                        }
                    }
                    appendNode('organization').with {
                        appendNode('name', 'eBay Inc.')
                        appendNode('url', 'https://ebay.com/')
                    }
                    appendNode('developers').with {
                        appendNode('developer').with {
                            appendNode('id', 'mcumings')
                            appendNode('name', 'Mike Cumings')
                            appendNode('email', 'mcumings@ebay.com')
                            appendNode('organization', 'eBay Inc.')
                            appendNode('organizationUrl', 'https://ebay.com/')
                            appendNode('timezone', '-8')
                        }
                    }
                }
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    dependsOn tasks.javadoc
}

